buildscript {
    repositories {
        maven { url 'http://repo.jfrog.org/artifactory/gradle-plugins' }
    }
    dependencies {
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '2.0.12')
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'artifactory'

    artifactory {
        contextUrl = 'http://repo.jfrog.org/artifactory' //The base Artifactory URL if not overridden by the publisher/resolver
        resolve {
            repository {
                repoKey = 'libs-releases'
                maven = true
            }
        }
    }

    group = 'com.jfrog.artifactory.client'
    version = '1-SNAPSHOT'

}


subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
    jar.dependsOn sourceJar

    artifacts {
        archives sourceJar, jar
    }

    test {
        useTestNG()
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '1.1'
}

idea {
    project {
        jdkName = '1.7'
        languageLevel = '1.7'
        wildcards += '?*.gradle'
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                // Use git
                def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
                vcsConfig.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}